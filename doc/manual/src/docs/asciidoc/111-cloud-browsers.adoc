= Cloud browser testing

When you want to perform web testing on multiple browsers and operating systems, it can be quite complicated to maintain machines for each of the target environments.
There are a few companies that provide "remote web browsers as a service", making it easy to do this sort of matrix testing without having to maintain the multiple browser installations yourself.
Geb provides easy integration with two such services, link:https://saucelabs.com/[SauceLabs] and link:http://www.browserstack.com/[BrowserStack].
This integration includes two parts: assistance with creating a driver in `GebConfig.groovy` and a Gradle plugin.

== Creating a driver

For both SauceLabs and BrowserStack, a special driver factory is provided that, given a browser specification as well as an username and access key, creates an instance of `RemoteWebDriver` configured
to use a browser in the cloud.
Examples of typical usage in `GebConfig.groovy` are included below.
They will configure Geb to run in SauceLabs/BrowserStack if the appropriate system property is set, and if not it will use whatever driver that is configured.
This is useful if you want to run the code in a local browser for development.
In theory you could use any system property to pass the browser specification but `geb.saucelabs.browser`/`geb.browserstack.browser` are also used by the Geb Gradle plugins, so it's a good idea to
stick with those property names.

The first parameter passed to the `create()` method is a ”browser specification“ and it should be a list of required browser capabilities in Java properties file format:

----
browserName=«browser name as per values of fields in org.openqa.selenium.remote.BrowserType»
platform=«platform as per enum item names in org.openqa.selenium.Platform»
version=«version»
----

Assuming you're using the following snippet in your `GebConfig.groovy` to execute your code via SauceLabs with Firefox 19 on Linux, you would set the `geb.saucelabs.browser` system property to:

----
browserName=firefox
platform=LINUX
version=19
----

and to execute it with IE 9 on Vista to:

----
browserName=internet explorer
platform=VISTA
version=9
----

Some browsers like Chrome automatically update to the latest version; for these browsers you don't need to specify the version as there's only one, and you would use something like:

----
browserName=chrome
platform=MAC
----

as the ”browser specification“. For a full list of available browsers, versions and operating systems refer to your cloud provider's documentation:

* https://saucelabs.com/docs/platforms/webdriver[SauceLabs platform list]
* http://www.browserstack.com/list-of-browsers-and-platforms?product=automate[BrowserStack Browsers and Platforms list]

Please note that Geb Gradle plugins can set the `geb.saucelabs.browser`/`geb.browserstack.browser` system properties for you using the aforementioned format.

Following the browser specification are the username and access key used to identify your account with the cloud provider.
The example uses two environment variables to access this information.
This is usually the easiest way of passing something secret to your build in open CI services like https://drone.io/[drone.io] or https://travis-ci.org/[Travis CI] if your code is public, but you can
use other mechanisms if desired.

You can optionally pass additional configuration settings by providing a Map to the `create()` method as the last parameter.
The configuration options available are described in your cloud provider's documentation:

* https://saucelabs.com/docs/additional-config[SauceLabs additional config]
* http://www.browserstack.com/automate/capabilities[BrowserStack Capabilities]

Finally, there is also link:api/geb/driver/CloudDriverFactory.html#create(java.lang.String,%20java.lang.String,%20Map%3CString,%20Object%3E)[an overloaded version of `create()` method] available that
doesn't take a string specification and allows you to simply specify all the required capabilities using a map.
This method might be useful if you just want to use the factory, but don't need the build level parametrization.

[[sauce-labs-driver-factory]]
=== `SauceLabsDriverFactory`

The following is an example of utilizing `SauceLabsDriverFactory` in `GebConfig.groovy` to configure a driver that will use a browser provided in the SauceLabs cloud.

[source,groovy]
----
def sauceLabsBrowser = System.getProperty("geb.saucelabs.browser")
if (sauceLabsBrowser) {
    driver = {
       def username = System.getenv("GEB_SAUCE_LABS_USER")
       assert username
       def accessKey = System.getenv("GEB_SAUCE_LABS_ACCESS_PASSWORD")
       assert accessKey
       new SauceLabsDriverFactory().create(sauceLabsBrowser, username, accessKey)
    }
}
----

[[browser-stack-driver-factory]]
=== `BrowserStackDriverFactory`

The following is an example of utilizing `BrowserStackDriverFactory` in `GebConfig.groovy` to configure a driver that will use a browser provided in the BrowserStack cloud.

[source,groovy]
----
def browserStackBrowser = System.getProperty("geb.browserstack.browser")
if (browserStackBrowser) {
    driver = {
       def username = System.getenv("GEB_BROWSERSTACK_USERNAME")
       assert username
       def accessKey = System.getenv("GEB_BROWSERSTACK_AUTHKEY")
       assert accessKey
       new BrowserStackDriverFactory().create(browserStackBrowser, username, accessKey)
    }
}
----

If using `localIdentifier` support:

[source,groovy]
----
def browserStackBrowser = System.getProperty("geb.browserstack.browser")
if (browserStackBrowser) {
    driver = {
       def username = System.getenv("GEB_BROWSERSTACK_USERNAME")
       assert username
       def accessKey = System.getenv("GEB_BROWSERSTACK_AUTHKEY")
       assert accessKey
       def localId = System.getenv("GEB_BROWSERSTACK_LOCALID")
       assert localId
       new BrowserStackDriverFactory().create(browserStackBrowser, username, accessKey, localId)
    }
}
----

== Gradle plugins

For both SauceLabs and BrowserStack, Geb provides a Gradle plugin which simplifies declaring the account and browsers that are desired, as well as configuring a tunnel to allow the cloud provider to
access local applications.
These plugins allow easily creating multiple `Test` tasks that will have the appropriate `geb.PROVIDER.browser` property set (where _PROVIDER_ is either `saucelabs` or `browserstack`).
The value of that property can be then passed in configuration file to <<sauce-labs-driver-factory>>/<<browser-stack-driver-factory>> as the ”browser specification“.
Examples of typical usage are included below.

=== geb-saucelabs plugin

Following is an example of using the geb-saucelabs Gradle plugin.

[source,groovy,subs="attributes,verbatim"]
----
apply plugin: "geb-saucelabs" //<1>

buildscript { //<2>
    repositories {
       mavenCentral()
    }
    dependencies {
       classpath 'org.gebish:geb-gradle:{geb-version}'
    }
}

repositories { //<3>
    maven { url "http://repository-saucelabs.forge.cloudbees.com/release" }
}

dependencies { //<4>
    sauceConnect "com.saucelabs:ci-sauce:1.81"
}

sauceLabs {
    browsers { //<5>
       firefox_linux_19
       chrome_mac
       delegate."internet explorer_vista_9"
       nexus4 { //<6>
         capabilities(
            browserName: "android",
            platform: "Linux",
            version: "4.4",
            deviceName: "LG Nexus 4"
         )
       }
    }
    task { //<7>
       testClassesDir = test.testClassesDir
       testSrcDirs = test.testSrcDirs
       classpath = test.classpath
    }
    account { //<8>
       username = System.getenv(SauceAccount.USER_ENV_VAR)
       accessKey = System.getenv(SauceAccount.ACCESS_KEY_ENV_VAR)
    }
    connect { //<9>
       port = 4444 //<10>
       additionalOptions = ['--proxy', 'proxy.example.com:8080'] //<11>
    }
}
----
<1> Apply the plugin to the build.
<2> Specify how to resolve the plugin.
<3> Declare a repository for resolving SauceConnect.
<4> Declare version of SauceConnect to be used as part of the `sauceConnect` configuration. This will be used by tasks that open a {sauce-connect} tunnel before
running the generated test tasks which means that the browsers in the cloud will have localhost pointing at the machine running the build.
<5> Declare that tests should run in 3 different browsers using the shorthand syntax; this will generate the following `Test` tasks: `firefoxLinux19Test`, `chromeMacTest` and
`internet explorerVista9Test`.
<6> Explicitly specify the required browser capabilities if the shorthand syntax doesn't allow you to express all needed capabilities; the example will generate a `Test` task named `nexus4Test`.
<7> Configure all of the generated test tasks; for each of them the closure is run with delegate set to a test task being configured.
<8> Pass credentials for {sauce-connect}.
<9> Additionally configure {sauce-connect} if desired.
<10> Override the port used by SauceConnect, defaults to 4445.
<11> Pass additional https://docs.saucelabs.com/reference/sauce-connect/#command-line-options[command line options] to SauceConnect.

[TIP]
====
You can use `allSauceLabsTests` task that will depend on all of the generated test tasks to run all of them during a build.
====

=== geb-browserstack

Following is an example of using the geb-browserstack Gradle plugin.

[source,groovy,subs="attributes,verbatim"]
----
apply plugin: "geb-browserstack" //<1>

buildscript { //<2>
    repositories {
       mavenCentral()
    }
    dependencies {
       classpath 'org.gebish:geb-gradle:{geb-version}'
    }
}

browserStack {
    application 'http://localhost:8080' //<3>
    forceLocal = true //<4>
    browsers { //<5>
       firefox_mac_19
       chrome_mac
       delegate."internet explorer_windows_9"
       nexus4 { //<6>
         capabilities browserName: "android", platform: "ANDROID", device: "Google Nexus 4"
       }
    }
    task { //<7>
       testClassesDir = test.testClassesDir
       testSrcDirs = test.testSrcDirs
       classpath = test.classpath
    }
    account { //<8>
       username = System.getenv(BrowserStackAccount.USER_ENV_VAR)
       accessKey = System.getenv(BrowserStackAccount.ACCESS_KEY_ENV_VAR)
    }
}
----
<1> Apply the plugin to the build.
<2> Specify how to resolve the plugin.
<3> Specify which urls the BrowserStack Tunnel should be able to access.
Multiple applications can be specified.
If no applications are specified, the tunnel will not be restricted to particular URLs.
<4> Configure BrowserStack tunnel to route all traffic via the local machine.
This configuration property controls the `-forcelocal` flag and the default value for it is `false`.
<5> Declare that tests should run in 3 different browsers using the shorthand syntax; this will generate the following `Test` tasks: `firefoxLinux19Test`, `chromeMacTest` and
`internet explorerVista9Test`.
<6> Explicitly specify the required browser capabilities if the shorthand syntax doesn't allow you to express all needed capabilities; the example will generate a `Test` task named `nexus4Test`.
<7> Configure all of the generated test tasks; for each of them the closure is run with delegate set to a test task being configured.
<8> Pass credentials for BrowserStack.

[TIP]
====
You can use `allBrowserStackTests` task that will depend on all of the generated test tasks to run all of them during a build.
====